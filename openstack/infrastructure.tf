provider "openstack" {
}

data "openstack_networking_subnet_v2" "subnet_1" {
}

data "openstack_compute_flavor_v2" "mgmt" {
  name = var.os_flavor_mgmt
}

data "openstack_compute_flavor_v2" "login" {
  name = var.os_flavor_login
}

data "openstack_compute_flavor_v2" "node" {
  name = var.os_flavor_node
}

data "external" "openstack_token" {
  program = ["sh", "${path.module}/gen_auth_token.sh"]
}

resource "openstack_compute_secgroup_v2" "secgroup_1" {
  name        = "${var.cluster_name}_secgroup"
  description = "Slurm security group"

  rule {
    from_port   = -1
    to_port     = -1
    ip_protocol = "icmp"
    self        = true
  }

  rule {
    from_port   = 1
    to_port     = 65535
    ip_protocol = "tcp"
    self        = true
  }

  rule {
    from_port   = 1
    to_port     = 65535
    ip_protocol = "udp"
    self        = true
  }

  dynamic "rule" {
    for_each = var.fw_ssh_filter
    content {
      from_port   = 22
      to_port     = 22
      ip_protocol = "tcp"
      cidr        = rule.value
    }
  }
}

resource "openstack_compute_keypair_v2" "keypair" {
  name       = "${var.cluster_name}_key"
  public_key = file(var.public_key_path)
}

resource "openstack_blockstorage_volume_v3" "shared_volume_1" {
  name = "shared_volume_1"
  size = var.shared_storage_size
}

resource "openstack_compute_instance_v2" "mgmt01" {
  name            = "mgmt01"
  flavor_id       = data.openstack_compute_flavor_v2.mgmt.id
  key_pair        = openstack_compute_keypair_v2.keypair.name
  security_groups = [openstack_compute_secgroup_v2.secgroup_1.name]
  user_data       = data.template_cloudinit_config.mgmt_config.rendered

  block_device {
    uuid                  = var.os_image_id
    source_type           = "image"
    volume_size           = var.home_fs_size
    boot_index            = 0
    destination_type      = "volume"
    delete_on_termination = true
  }
}

resource "openstack_compute_volume_attach_v2" "va_1" {
  instance_id = openstack_compute_instance_v2.mgmt01.id
  volume_id   = openstack_blockstorage_volume_v3.shared_volume_1.id
  device      = "/dev/vdb"
}

locals {
  mgmt01_ip = openstack_compute_instance_v2.mgmt01.network[0].fixed_ip_v4
  public_ip = openstack_networking_floatingip_v2.fip_1.address
  cidr      = data.openstack_networking_subnet_v2.subnet_1.cidr
}

resource "openstack_compute_instance_v2" "login01" {
  name     = "${var.cluster_name}01"
  image_id = var.os_image_id

  flavor_id       = data.openstack_compute_flavor_v2.login.id
  key_pair        = openstack_compute_keypair_v2.keypair.name
  security_groups = [openstack_compute_secgroup_v2.secgroup_1.name]
  user_data       = data.template_cloudinit_config.login_config.rendered
}

resource "openstack_compute_instance_v2" "node" {
  count    = var.nb_nodes
  name     = "node${count.index + 1}"
  image_id = var.os_image_id

  flavor_id       = data.openstack_compute_flavor_v2.node.id
  key_pair        = openstack_compute_keypair_v2.keypair.name
  security_groups = [openstack_compute_secgroup_v2.secgroup_1.name]
  user_data = element(
    data.template_cloudinit_config.node_config.*.rendered,
    count.index,
  )
}

resource "openstack_networking_floatingip_v2" "fip_1" {
  pool = var.os_external_network
}

resource "openstack_compute_floatingip_associate_v2" "fip_1" {
  floating_ip = openstack_networking_floatingip_v2.fip_1.address
  instance_id = openstack_compute_instance_v2.login01.id
}

